start:
	say "Food lover !"
	say Image ("https://media.giphy.com/media/8c10orHgs84mboQ2Un/giphy.gif")
	say "In which city are you looking to find a restaurant ?"
	hold
	say Image ("https://media.giphy.com/media/4GXUa4U05Q0JAM972c/giphy.gif")
	use Fn("openTrip", action = "getCityCoord", query = event) as coord
	use Fn("openTrip", action = "getAttractionList", kind = "restaurants,fast_food", radius = "1000", lat = "{{coord.lat}}", lon = "{{coord.lon}}" ) as r
	say "Our top restaurants that we can propose you ðŸ¤©"
	use [] as array
	foreach (e, i) in r.features {
		if (i < 5){
			use Fn("openTrip", action = "getAttractionDetails", xid = "{{e.properties.xid}}") as det
			use Fn("utils", action="push", array = array, element = det) as array
		}
	}

	foreach (e, i) in array {
		say Button("{{e.name}}")
	}
	hold

	foreach (e, i) in array {
		if (event match "{{e.name}}") {
			say Image("{{e.preview.source}}")
			say "{{e.wikipedia_extracts.text}}"
			use Fn("utils", action="split", string = "{{e.url}}", separator = ";") as links
			say Url("{{links[0]}}")
		}
	}

	use Button("Thank you very much !") as thanks
	use Button("An other information") as information
	say Question(
		title = "How can I help you now ? ðŸ’ðŸ¼â€â™€ï¸",
		buttons = [thanks, information]
  )
	hold
	if(event match thanks) {
		say "Good bye ! See you later !"
		say Image("https://media.giphy.com/media/UrcXN0zTfzTPi/giphy.gif")
		goto end
	}
	if(event match information) {
		goto flow Redefault
	}
	goto end
  